buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "https://maven.fabricmc.net/" }
        maven { url = "https://maven.architectury.dev/" }
        maven { url = "https://maven.minecraftforge.net/" }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

configurations {
    library
    implementation.extendsFrom library
}

minecraft.runs.all {
    // Tell forge to include your libraries in the classpath
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}


println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2022.11.06-1.18.2'
    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'watermedia'
            arg '--mixin.config'
            arg 'watermedia.mixin.json'

            mods {
                watermedia {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'watermedia'
            arg '--mixin.config'
            arg 'watermedia.mixin.json'

            mods {
                watermedia {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://repo.spongepowered.org/maven" }
    maven { url "https://maven.tterrag.com/" }
    maven {
        url "https://www.cursemaven.com"
        content { includeGroup "curse.maven" }
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mcV}-${project.fgV}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // FABRIC LOADER
    implementation "net.fabricmc:fabric-loader:0.14.19"

    library 'net.java.dev.jna:jna:5.13.0'
    library 'com.github.sealedtx:java-youtube-downloader:3.2.3'
    library ('com.github.walkyst:lavaplayer-fork:1.4.1') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    library 'com.github.aikaterna:lavaplayer-natives:original-SNAPSHOT'
    library 'com.mpatric:mp3agic:0.9.1'
    library("uk.co.caprica:vlcj:5.0.0-SNAPSHOT") {
        exclude group: 'net.java.dev.jna', module: 'jna'
        exclude group: 'com.sun.jna', module: 'jna'
        exclude group: 'net.java.dev.jna', module: 'jna-platform'
    }

    library("uk.co.caprica:vlcj-natives:5.0.0-SNAPSHOT") {
        exclude group: 'net.java.dev.jna', module: 'jna'
        exclude group: 'com.sun.jna', module: 'jna'
        exclude group: 'net.java.dev.jna', module: 'jna-platform'
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.library]

    // Add relocation rules for each dependency
    relocate 'com.github.sealedtx', 'me.srrapero720.sealedtx'
    relocate 'com.github.walkyst', 'me.srrapero720.walkyst'
    relocate 'com.github.aikaterna', 'me.srrapero720.aikaterna'
    relocate 'com.mpatric', 'me.srrapero720.mpatric'
    relocate 'uk.co.caprica', 'me.srrapero720.caprica'
}

mixin { add sourceSets.main, "watermedia.mixin-refmap.json" }

// Make the reobfJar task depend on shadowJar
tasks.build.dependsOn shadowJar

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : project.author,
                "Specification-Version"   : project.version, // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : project.author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar // Replace 'jar' with 'shadowJar'
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
}